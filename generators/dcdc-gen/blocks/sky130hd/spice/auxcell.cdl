* dcdcInst: AUX CELL DCDC_COMP
.subckt DCDC_COMP VGND VNB VPB VPWR clk neg_in pos_in out
.model comp SW(Ron=0.1 Roff=1e12 Vt=0 Vh=0)
S1 vvdd out pos_in neg_in comp
v1 vvdd 0 1.8
.ends DCDC_COMP

* 2:1 stage: PMOS SWITCH
.subckt DCDC_XSW_PMOS VGND VNB VPB VPWR clk clkb vIN vOUT0 vOUT1
x0 int_sw0 int_sw1 vIN VPB sky130_fd_pr__pfet_01v8 w=420000u l=150000u
x1 int_sw1 int_sw0 vIN VPB sky130_fd_pr__pfet_01v8 w=420000u l=150000u
x2 vOUT0 int_sw0 vIN VPB sky130_fd_pr__pfet_01v8 w=1000000u l=150000u m=2
x3 vOUT1 int_sw1 vIN VPB sky130_fd_pr__pfet_01v8 w=1000000u l=150000u m=2
x4 clkb int_sw0 clkb clkb sky130_fd_pr__pfet_01v8 w=3000000u l=2000000u m=1
x5 clk int_sw1 clk clk sky130_fd_pr__pfet_01v8 w=3000000u l=2000000u m=1
.ends DCDC_XSW_PMOS

* 2:1 stage: NMOS SWITCH
.subckt DCDC_XSW_NMOS VGND VNB VPB VPWR clk clkb vIN vOUT0 vOUT1
x0 int_sw0 int_sw1 vIN VNB sky130_fd_pr__nfet_01v8 w=420000u l=150000u
x1 int_sw1 int_sw0 vIN VNB sky130_fd_pr__nfet_01v8 w=420000u l=150000u
x2 vOUT0 int_sw0 vIN VNB sky130_fd_pr__nfet_01v8 w=420000u l=150000u m=2
x3 vOUT1 int_sw1 vIN VNB sky130_fd_pr__nfet_01v8 w=420000u l=150000u m=2
x4 clkb int_sw0 clkb clkb sky130_fd_pr__pfet_01v8 w=3000000u l=2000000u m=1
x5 clk int_sw1 clk clk sky130_fd_pr__pfet_01v8 w=3000000u l=2000000u m=1
.ends DCDC_XSW_NMOS

*2:1 converter: DCDC_HUNIT_CONV2to1
.subckt DCDC_HUNIT_CONV2to1 VGND VNB VPB VPWR clk0 clk0b clk1 clk1b y0 y1 vhigh vlow
x0 VGND VNB VPB VPWR clk1b clk0b vhigh y0 y1 DCDC_XSW_PMOS 
x1 VGND VNB VPB VPWR clk0 clk1 vlow y0 y1 DCDC_XSW_NMOS 
.ends DCDC_HUNIT_CONV2to1

*2:1 converter: DCDC_CONV2TO1
.subckt DCDC_CONV2TO1 VGND VNB VPB VPWR clk0 clk0b clk1 clk1b y0_top y1_top y0_bot y1_bot vhigh vmid vlow 
x0 VGND VNB VPB VPWR clk0 clk0b clk1 clk1b y0_top y1_top vhigh vmid DCDC_HUNIT_CONV2to1
x1 VGND VNB VPB VPWR clk0 clk0b clk1 clk1b y0_bot y1_bot vmid vlow DCDC_HUNIT_CONV2to1
.ends DCDC_CONV2TO1 

* 2:1 stage: unit cap
.subckt DCDC_CAP_UNIT VGND VNB VPB VPWR bot top
c0 top bot 1p
.ends DCDC_CAP_UNIT

* power mux: DCDC_MUX
.subckt DCDC_MUX_PMOS VGND VNB VPB VPWR SEL SEL_INV VIN VOUT
x1  VIN SEL_INV VOUT VPWR sky130_fd_pr__pfet_01v8 w=6000000u l=150000u m=8
x2  VOUT SEL     VIN VPWR sky130_fd_pr__pfet_01v8 w=6000000u l=150000u m=8
.ends DCDC_MUX_PMOS

.subckt DCDC_MUX_NMOS VGND VNB VPB VPWR SEL SEL_INV VIN VOUT
x1  VIN SEL_INV VOUT VGND sky130_fd_pr__nfet_01v8 w=6000000u l=150000u m=8
x2  VOUT SEL     VIN VGND sky130_fd_pr__nfet_01v8 w=6000000u l=150000u m=8
.ends DCDC_MUX_NMOS

.subckt DCDC_MUX_TGATE VGND VNB VPB VPWR SEL SEL_INV VIN VOUT
x1  VOUT SEL_INV VIN VPWR sky130_fd_pr__pfet_01v8 w=6000000u l=150000u m=8
x2  VOUT SEL     VIN VGND sky130_fd_pr__nfet_01v8 w=6000000u l=150000u m=8
.ends DCDC_MUX_TGATE

.subckt DCDC_MUX VGND VNB VPB VPWR SEL_H SEL_INV_H SEL_L SEL_INV_L VIN VOUT_H VOUT_L
* Top MUX rail
x0 VGND VNB VPB VPWR SEL_INV_H SEL_INV_H VPWR VOUT_H DCDC_MUX_PMOS
* Top MUX mid
x1 VGND VNB VPB VPWR SEL_INV_H SEL_H VIN VOUT_H DCDC_MUX_TGATE
* Bot MUX mid
x2 VGND VNB VPB VPWR SEL_L SEL_INV_L VIN VOUT_L DCDC_MUX_TGATE
* Bot MUX rail
x3 VGND VNB VPB VPWR SEL_INV_L SEL_INV_L VGND VOUT_L DCDC_MUX_NMOS
.ends DCDC_MUX